apply plugin: 'java'
apply plugin: 'groovy'

repositories {
  mavenLocal()
  mavenCentral()
}

String seleniumVersion = "2.35.0"

dependencies {
  testCompile "org.gebish:geb-spock:0.9.1"
  testCompile "org.seleniumhq.selenium:selenium-firefox-driver:${seleniumVersion}"
  testCompile "org.seleniumhq.selenium:selenium-support:${seleniumVersion}"

  testCompile 'org.codehaus.groovy.modules.remote:remote-transport-http:0.5'

  testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
}

sourceSets {
  test {
    groovy {
      srcDirs = ['test/functional']
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.8'
}

test {
  // Disable Gradle's up-to-date checking so it always runs the tests, even if I didn't update any test files
  outputs.upToDateWhen { false }

  maxParallelForks = 2

  String grailsServerPort = System.getProperty('server.port', '8080')

  systemProperty "geb.build.reportsDir", file("$buildDir/test-results-functional/geb")
  systemProperty "geb.build.baseUrl", "http://localhost:${grailsServerPort}/geb-parallel-gradle/"
}

test.doFirst {
  // Using the Grails wrapper to start the Grails application in the 'test' environment so the remote control
  // plugin is enabled and my remote util bean is injected from resources.groovy
  println "Starting grails app"
  execCommandAndWaitForStatusString("grailsw.bat test run-app", 'Server running.')
}

task stopGrails << {
  // Grails will shut down when it sees a file called ".kill-run-app"
  // See http://stackoverflow.com/a/4665331
  new File(".kill-run-app").text = ""
}

test.finalizedBy stopGrails

/**
 * Credit for this method goes to Tomas Lin
 * http://fbflex.wordpress.com/2013/03/14/gradle-madness-execwait-a-task-that-waits-for-commandline-calls-to-be-ready/
 */
Process execCommandAndWaitForStatusString(String command, String readyStatusString) {
  ProcessBuilder builder = new ProcessBuilder(command.split(' '))
  builder.redirectErrorStream(true)
  builder.directory(new File("."))
  Process process = builder.start()

  InputStream processStdout = process.getInputStream()
  BufferedReader reader = new BufferedReader(new InputStreamReader(processStdout))

  def line
  while ((line = reader.readLine()) != null) {
    println line
    if (line.contains(readyStatusString)) {
      println "'${command}' is ready"
      break;
    }
  }

  return process
}
